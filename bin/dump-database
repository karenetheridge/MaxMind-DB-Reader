#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Long;
use IO::Handle;
use JSON;
use MaxMind::DB::Reader;
use Net::Works::Network;

sub main {
    my $file;
    GetOptions(
        'file:s' => \$file,
    );

    my $reader = MaxMind::DB::Reader->new( file => $file );
    my $ip_version = $reader->ip_version();

    # For large databases this iteration could take a long time so it's good
    # to send output as it's available.
    STDOUT->autoflush(1);
    $reader->iterate_search_tree( sub { _dump_entry( $ip_version, @_ ) } );
}

my $JSON = JSON->new()->utf8->allow_nonref()->pretty();

sub _dump_entry {
    my $ip_version = shift;
    my $ipnum      = shift;
    my $depth      = shift;
    my $entry_data = shift;

    my $network = Net::Works::Network->new_from_integer(
        integer     => $ipnum,
        mask_length => $depth,
        ip_version  => $ip_version,
    );

    my $output = $network->as_string() . "\n";
    my $encoded = $JSON->encode($entry_data);
    $encoded =~ s/^/  /mg;
    $output .= $encoded . "\n";

    print $output;
}

main();
