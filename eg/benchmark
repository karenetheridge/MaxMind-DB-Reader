#!/usr/bin/env perl

use strict;
use warnings;

use Carp;
use Data::Printer;
use Getopt::Long;
use MaxMind::DB::Reader;
use Module::Runtime qw(use_module);
use Time::HiRes qw( gettimeofday tv_interval );
use Try::Tiny;

sub main {
    my $file;
    my $iterations = 10000;
    my $reader;

    GetOptions(
        'file:s'       => \$file,
        'iterations:i' => \$iterations,
        'reader:s'     => \$reader,
    );

    $reader = get_reader( reader => $reader, file => $file );

    my $start = [gettimeofday];
    for my $i (1 .. $iterations) {
        print "$i\n" if $i % 1000 == 0;

        # get a random IPv4 address. Not using Net::Works due to possible
        # speed issues
        my $ip = join '.', unpack 'C4', pack 'N', int(rand(2**32-1));

        # Catch exceptions from unknown addresses
        try {
           my $record = $reader->record_for_address($ip);
        };
    }
    my $end = [gettimeofday];
    my $duration = tv_interval( $start, $end );

    printf
      "Looked up %d records in %f seconds, a speed of %f lookups / second\n",
      $iterations, $duration, $iterations / $duration;
}

main();

sub get_reader {
    my %args = @_;

    my $reader = $args{reader};
    my $file   = $args{file};

    if ( !$reader ) {
        return MaxMind::DB::Reader->new( file => $file );
    }
    elsif ( $reader =~ qr/^pp$/i ) {
        use_module('MaxMind::DB::Reader::PP');
        return MaxMind::DB::Reader::PP->new( file => $file );
    }
    elsif ( $reader =~ qr/^xs$/i ) {
        use_module('MaxMind::DB::Reader::XS');
        return MaxMind::DB::Reader::XS->new( file => $file );
    }
    else {
        croak "If passed, reader must be either PP or XS, but you passed '$reader'\n"
    }
}
